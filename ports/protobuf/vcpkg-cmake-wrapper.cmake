if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.3)
    cmake_policy(PUSH)
    cmake_policy(SET CMP0057 NEW)
    if(NOT "CONFIG" IN_LIST ARGS AND NOT "NO_MODULE" IN_LIST ARGS)
        if("@VCPKG_LIBRARY_LINKAGE@" STREQUAL "static")
            set(Protobuf_USE_STATIC_LIBS ON)
        else()
            set(Protobuf_USE_STATIC_LIBS OFF)
        endif()
    endif()
    cmake_policy(POP)
endif()
_find_package(${ARGS})

if(Protobuf_FOUND AND VCPKG_HOST_TRIPLET AND NOT VCPKG_HOST_TRIPLET STREQUAL VCPKG_TARGET_TRIPLET)
    foreach(prop IMPORTED_LOCATION IMPORTED_LOCATION_RELEASE IMPORTED_LOCATION_DEBUG)
        get_target_property(_protoc_exe_path protobuf::protoc ${prop})
        if(_protoc_exe_path)
            string(REPLACE "/${VCPKG_TARGET_TRIPLET}/" "/${VCPKG_HOST_TRIPLET}/" _protoc_exe_path "${_protoc_exe_path}")
            if(NOT EXISTS "${_protoc_exe_path}")
                message(WARNING "${_protoc_exe_path} is not installed. Cross compile might fail. Try to install protobuf:${VCPKG_HOST_TRIPLET}")
            else()
                set_target_properties(protobuf::protoc PROPERTIES ${prop} "${_protoc_exe_path}")
            endif()
        endif()
    endforeach()
endif()
